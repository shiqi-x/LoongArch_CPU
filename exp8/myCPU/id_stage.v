module id_stage(
    input  wire         clk,
    input  wire         reset,
    // valid/allow
    input  wire         if_to_id_valid,
    input  wire         allowin_exe,
    output wire         id_to_exe_valid,
    output wire         allowin_id,
    // if_to_id
    input  wire  [31:0] inst_if,
    input  wire  [31:0] pc_if,
    // exe_to_id
    input  wire  [ 4:0] rd_dest_exe,
    // mem_to_id
    input  wire  [ 4:0] rd_dest_mem,
    // wb_to_id
    input  wire  [ 4:0] rd_dest_wb,
    input  wire         rf_we_wb,
    input  wire  [ 4:0] rf_waddr_wb,
    input  wire  [31:0] rf_wdata_wb,
    // id_to_if
    output wire         br_taken_id,
    output wire  [31:0] br_target_id,
    output wire         br_taken_cancle_id,
    // id_to_exe
    output wire  [31:0] pc_id,
    output wire         rf_we_id,
    output wire  [ 4:0] rf_waddr_id,
    output wire         res_from_mem_id,
    output wire  [11:0] alu_op_id,
    output wire  [31:0] alu_src1_id,
    output wire  [31:0] alu_src2_id,
    output wire  [31:0] data_sram_wdata_id,
    output wire  [ 3:0] data_sram_we_id
);

// 单周期的信号
wire        src1_is_pc;
wire        src2_is_imm;
wire        dst_is_r1;
wire        gr_we;
wire        mem_we;
wire        src_reg_is_rd;
wire        src2_is_4;
wire        sel_rk_src_zero;
wire        sel_rj_src_zero;
wire [ 4:0] rk_src;
wire [ 4:0] rj_src;
wire        block;
wire        br_taken_cancle;
wire [31:0] rj_value;
wire [31:0] rkd_value;
wire [31:0] imm;
wire [31:0] br_offs;
wire [31:0] jirl_offs;

wire [ 5:0] op_31_26;
wire [ 3:0] op_25_22;
wire [ 1:0] op_21_20;
wire [ 4:0] op_19_15;
wire [ 4:0] rd;
wire [ 4:0] rj;
wire [ 4:0] rk;
wire [11:0] i12;
wire [19:0] i20;
wire [15:0] i16;
wire [25:0] i26;

wire [63:0] op_31_26_d;
wire [15:0] op_25_22_d;
wire [ 3:0] op_21_20_d;
wire [31:0] op_19_15_d;

wire        inst_add_w;
wire        inst_sub_w;
wire        inst_slt;
wire        inst_sltu;
wire        inst_nor;
wire        inst_and;
wire        inst_or;
wire        inst_xor;
wire        inst_slli_w;
wire        inst_srli_w;
wire        inst_srai_w;
wire        inst_addi_w;
wire        inst_ld_w;
wire        inst_st_w;
wire        inst_jirl;
wire        inst_b;
wire        inst_bl;
wire        inst_beq;
wire        inst_bne;
wire        inst_lu12i_w;

wire        need_ui5;
wire        need_si12;
wire        need_si16;
wire        need_si20;
wire        need_si26;

wire [ 4:0] rf_raddr1;
wire [31:0] rf_rdata1;
wire [ 4:0] rf_raddr2;
wire [31:0] rf_rdata2;

// 流水线的信号
reg         valid_id;
wire        ready_go_id;

reg  [31:0] inst;
reg  [31:0] pc;

// 信号处理
always @(posedge clk) begin
    if (reset) begin
        valid_id <= 1'b0;
    end
    else if (br_taken_cancle) begin
        valid_id <= 1'b0;
    end
    else if (allowin_id) begin
        valid_id <= if_to_id_valid;
    end

    if (if_to_id_valid && allowin_id) begin
        inst <= inst_if;
        pc <= pc_if;
    end
end

assign op_31_26  = inst[31:26];
assign op_25_22  = inst[25:22];
assign op_21_20  = inst[21:20];
assign op_19_15  = inst[19:15];

assign rd   = inst[ 4: 0];
assign rj   = inst[ 9: 5];
assign rk   = inst[14:10];

assign i12  = inst[21:10];
assign i20  = inst[24: 5];
assign i16  = inst[25:10];
assign i26  = {inst[ 9: 0], inst[25:10]};

decoder_6_64 u_dec0(.in(op_31_26 ), .out(op_31_26_d ));
decoder_4_16 u_dec1(.in(op_25_22 ), .out(op_25_22_d ));
decoder_2_4  u_dec2(.in(op_21_20 ), .out(op_21_20_d ));
decoder_5_32 u_dec3(.in(op_19_15 ), .out(op_19_15_d ));

assign inst_add_w  = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h00];
assign inst_sub_w  = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h02];
assign inst_slt    = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h04];
assign inst_sltu   = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h05];
assign inst_nor    = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h08];
assign inst_and    = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h09];
assign inst_or     = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h0a];
assign inst_xor    = op_31_26_d[6'h00] & op_25_22_d[4'h0] & op_21_20_d[2'h1] & op_19_15_d[5'h0b];
assign inst_slli_w = op_31_26_d[6'h00] & op_25_22_d[4'h1] & op_21_20_d[2'h0] & op_19_15_d[5'h01];
assign inst_srli_w = op_31_26_d[6'h00] & op_25_22_d[4'h1] & op_21_20_d[2'h0] & op_19_15_d[5'h09];
assign inst_srai_w = op_31_26_d[6'h00] & op_25_22_d[4'h1] & op_21_20_d[2'h0] & op_19_15_d[5'h11];
assign inst_addi_w = op_31_26_d[6'h00] & op_25_22_d[4'ha];
assign inst_ld_w   = op_31_26_d[6'h0a] & op_25_22_d[4'h2];
assign inst_st_w   = op_31_26_d[6'h0a] & op_25_22_d[4'h6];
assign inst_jirl   = op_31_26_d[6'h13];
assign inst_b      = op_31_26_d[6'h14];
assign inst_bl     = op_31_26_d[6'h15];
assign inst_beq    = op_31_26_d[6'h16];
assign inst_bne    = op_31_26_d[6'h17];
assign inst_lu12i_w= op_31_26_d[6'h05] & ~inst[25];

assign rj_eq_rd = (rj_value == rkd_value);
assign br_taken_id = (   inst_beq  &&  rj_eq_rd
                   || inst_bne  && !rj_eq_rd
                   || inst_jirl
                   || inst_bl
                   || inst_b                ) && valid_id;
assign br_target_id = (inst_beq || inst_bne || inst_bl || inst_b) ? (pc + br_offs) :
                                                   /*inst_jirl*/ (rj_value + jirl_offs);

assign alu_op_id[ 0] = inst_add_w | inst_addi_w | inst_ld_w | inst_st_w
                    | inst_jirl | inst_bl;
assign alu_op_id[ 1] = inst_sub_w;
assign alu_op_id[ 2] = inst_slt;
assign alu_op_id[ 3] = inst_sltu;
assign alu_op_id[ 4] = inst_and;
assign alu_op_id[ 5] = inst_nor;
assign alu_op_id[ 6] = inst_or;
assign alu_op_id[ 7] = inst_xor;
assign alu_op_id[ 8] = inst_slli_w;
assign alu_op_id[ 9] = inst_srli_w;
assign alu_op_id[10] = inst_srai_w;
assign alu_op_id[11] = inst_lu12i_w;

assign src1_is_pc    = inst_jirl | inst_bl;
assign src2_is_imm   = inst_slli_w |
                       inst_srli_w |
                       inst_srai_w |
                       inst_addi_w |
                       inst_ld_w   |
                       inst_st_w   |
                       inst_lu12i_w|
                       inst_jirl   |
                       inst_bl     ;

assign need_ui5   =  inst_slli_w | inst_srli_w | inst_srai_w;
assign need_si12  =  inst_addi_w | inst_ld_w | inst_st_w;
assign need_si16  =  inst_jirl | inst_beq | inst_bne;
assign need_si20  =  inst_lu12i_w;
assign need_si26  =  inst_b | inst_bl;

assign imm = src2_is_4 ? 32'h4                      :
             need_si20 ? {i20[19:0], 12'b0}         :
/*need_ui5 || need_si12*/{{20{i12[11]}}, i12[11:0]} ;

assign br_offs = need_si26 ? {{ 4{i26[25]}}, i26[25:0], 2'b0} :
                             {{14{i16[15]}}, i16[15:0], 2'b0} ;
assign jirl_offs = {{14{i16[15]}}, i16[15:0], 2'b0};

assign rj_value  = rf_rdata1;
assign rkd_value = rf_rdata2;
assign data_sram_wdata_id = rkd_value;

assign res_from_mem_id  = inst_ld_w;
assign dst_is_r1        = inst_bl;
assign gr_we            = ~inst_st_w & ~inst_beq & ~inst_bne & ~inst_b;
assign mem_we           = inst_st_w;
assign src_reg_is_rd    = inst_beq | inst_bne | inst_st_w;
assign src2_is_4        = inst_jirl | inst_bl;
assign sel_rj_src_zero  = inst_b | inst_bl | inst_lu12i_w;
assign sel_rk_src_zero  = inst_addi_w |
                          inst_ld_w   |
                          inst_slli_w |
                          inst_srli_w |
                          inst_srai_w |
                          inst_jirl   |
                          inst_b      |
                          inst_bl     |
                          inst_lu12i_w;
assign rf_waddr_id      = dst_is_r1 ? 5'd1 : rd;
assign rk_src           = {5{~sel_rk_src_zero}} & {5{valid_id}} & rf_waddr_id;
assign rj_src           = {5{~sel_rj_src_zero}} & {5{valid_id}} & rj;

assign rf_raddr1 = rj;
assign rf_raddr2 = src_reg_is_rd ? rd : rk ;
regfile u_regfile(
    .clk    (clk         ),
    .raddr1 (rf_raddr1   ),
    .rdata1 (rf_rdata1   ),
    .raddr2 (rf_raddr2   ),
    .rdata2 (rf_rdata2   ),
    .we     (rf_we_wb    ),
    .waddr  (rf_waddr_wb ),
    .wdata  (rf_wdata_wb )
    );

assign alu_src1_id = src1_is_pc  ? pc[31:0] : rj_value;
assign alu_src2_id = src2_is_imm ? imm : rkd_value;

assign rf_we_id = gr_we && valid_id;
assign data_sram_we_id = mem_we && valid_id ? 4'hf : 4'h0;

assign pc_id = pc;

assign block = ((rk_src == rd_dest_exe) & (|rk_src)) ||
               ((rk_src == rd_dest_mem) & (|rk_src)) ||
               ((rk_src == rd_dest_wb ) & (|rk_src)) ||
               ((rj_src == rd_dest_exe) & (|rj_src)) ||
               ((rj_src == rd_dest_mem) & (|rj_src)) ||
               ((rj_src == rd_dest_wb ) & (|rj_src)) ;

assign br_taken_cancle    = ~block && br_taken_id;
assign br_taken_cancle_id = br_taken_cancle; 

// 流水线结构相关
assign ready_go_id = ~block;
assign allowin_id = !valid_id || ready_go_id && allowin_exe;
assign id_to_exe_valid = valid_id && ready_go_id;

endmodule